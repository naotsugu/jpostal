import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'java'
    id 'application'
    id "com.vanniktech.maven.publish" version "0.34.0"
}

group = 'com.mammb'
version = '0.5.0'

repositories {
    mavenCentral()
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.3')
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation('org.assertj:assertj-core:3.24.2')
}


application {
    mainClass.set("com.mammb.code.jpostal.App")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.mammb.code.jpostal.App'
    }
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates(group.toString(), "jpostal", version.toString())
    mavenPublishing {
        configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
    }
    pom {
        name = "JPostal"
        description = "Japan postal code dictionary utility."
        inceptionYear = "2020"
        url = "https://github.com/naotsugu/jpostal"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "naotsugu"
                name = "Naotsugu Kobayashi"
                url = "https://github.com/naotsugu/"
            }
        }
        scm {
            url = "https://github.com/naotsugu/jpostal/"
            connection = "scm:git:git://github.com/naotsugu/jpostal.git"
            developerConnection = "scm:git:ssh://git@github.com/naotsugu/jpostal.git"
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
