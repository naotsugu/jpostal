plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'signing'
}

group = 'com.mammb'
version = '0.2.0'

repositories {
    mavenCentral()
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.0")
    testImplementation("org.assertj:assertj-core:3.23.1")
}


application {
    mainClass.set("com.mammb.code.jpostal.App")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.mammb.code.jpostal.App'
    }
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(9)
    }
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jpostal'
            from components.java
            pom {
                name = 'JPostal'
                description = 'Japan postal code dictionary utility'
                url = 'https://github.com/naotsugu/jpostal'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'naotsugu'
                        name = 'Naotsugu Kobayashi'
                        email = 'naotsugukobayashi@gmail.com'
                    }
                }
                scm {
                    connection = 'git@github.com:naotsugu/jpostal.git'
                    developerConnection = 'git@github.com:naotsugu/jpostal.git'
                    url = 'https://github.com/naotsugu/jpostal'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['sonatypeUsername'] ?: ""
                password = project.properties['sonatypePassword'] ?: ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
